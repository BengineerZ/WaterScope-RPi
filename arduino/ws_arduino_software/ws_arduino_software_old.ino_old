#include <math.h>

#include <PID_v1.h>
double PID_setpoint, PID_input, PID_output;
//Define the aggressive and conservative Tuning Parameters
// https://robotics.stackexchange.com/questions/9786/how-do-the-pid-parameters-kp-ki-and-kd-affect-the-heading-of-a-differential
double Kp = 18, Ki = 1, Kd = 1;
//Specify the links and initial tuning parameters
PID myPID(&PID_input, &PID_output, &PID_setpoint, Kp, Ki, Kd, DIRECT);

#include <OneWire.h>
#include <DallasTemperature.h>
#define ONE_WIRE_BUS 2
OneWire oneWire(ONE_WIRE_BUS);
//OneWire oneWire1(ONE_WIRE_BUS_2);
// Pass our oneWire reference to Dallas Temperature.

// multiple sensors https://lastminuteengineers.com/multiple-ds18b20-arduino-tutorial/
DallasTemperature sensors(&oneWire);
int sensor_counts;

// LEDs
#include <Adafruit_NeoPixel.h>
#define LED_PIN 4
#define NUMPIXELS 12
Adafruit_NeoPixel LED = Adafruit_NeoPixel(NUMPIXELS, LED_PIN, NEO_GRB + NEO_KHZ800);
// starting LED colour
int r = 5, g = 5, b = 5;

//motor controls
#include <Stepper.h>
// controls the heating
#define HEATER_PIN 3
#define END_STOP_PIN_OPT A0
#define END_STOP_PIN_CAR A0
int end_stop = 0;
// the carousel rotatry motor
Stepper stepper_optics(32, 11, 13, 12, 10);
float stepper_optics_ratio = 5;
//  the optics stage motor pins
Stepper stepper_carousel(32, 7, 9, 8, 6);
// use this value to convert the motor movement to the angles
float stepper_carousel_ratio = 6;

int speed_optics = 1000;
int speed_carousel = 1000;
// DEBUG: this is wrong
float absolute_pos_optics = 0;
int absolute_pos_carousel = 0;

// Threading
#include <Thread.h>
#include <ThreadController.h>
// ThreadController that will controll all threads
ThreadController thread_controller = ThreadController();
Thread temp_thread = Thread();
Thread read_serial_thread = Thread();
Thread led_thread = Thread();
Thread optics_motor_thread = Thread();
Thread carousel_motor_thread = Thread();
// Thread hisThread = Thread();

// global varialbes for the code
String serial_input;
float starting_time;

void setup(void)
{
    // start serial port
    Serial.begin(9600);
    Serial.setTimeout(50);

    //init the LED_RING, but not shining
    LED.begin();
    LED_colour(0, 0, 0);
    // init the temperature sensors
    sensors.begin();
    sensor_counts = sensors.getDeviceCount();

    // init the heater PID
    pinMode(HEATER_PIN, OUTPUT);
    // PID control for heater
    myPID.SetMode(AUTOMATIC);
    PID_setpoint = 0; //set the temperature
    myPID.SetOutputLimits(1, 255);

    // Configure Threads
    temp_thread.onRun(read_temp_adjust_heating);
    temp_thread.setInterval(2000);
    read_serial_thread.onRun(read_serial);
    read_serial_thread.setInterval(200);
    // Adds  threads to the controller
    thread_controller.add(&read_serial_thread);
    thread_controller.add(&temp_thread); // & to pass the pointer to it

    Serial.println("Recommand to home the stage if this is the first run");
    // DEBUG: uncomment on deployment
    // home_stage();
    // move to the optimal position
    // move_stage(3000,speed);
    // measure a starting time
    starting_time = millis();
}

void loop(void)
{
    //read_serial();
    // run the threads
    thread_controller.run();
}

void read_serial(void)
{
    // To read serial input
    if (Serial.available())
    {
        // https://stackoverflow.com/questions/42863973/arduino-readstring-code-runs-slow
        // speed up the Serial.readString
        serial_input = Serial.readStringUntil('\n');
        serial_condition(serial_input);
    }
}

void read_temp_adjust_heating(void)
{
    // an array to record sensors temp
    float sensors_temp[sensor_counts];
    // declare relavent variables
    float temperature_sum = 0;
    float ave_temperature;
    // a variable to store error of the sensor connection
    bool sensors_error = false;

    // call sensors.requestTemperatures() to issue a global temperature
    // request to all devices on the bus
    sensors.requestTemperatures();

    //prints time since program started
    float time = (millis() - starting_time) / float(1000);
    Serial.println(String(time) + " s");
    // Iterate through multiple sensors
    // Record and display temperature from each sensor
    for (int i = 0; i < sensor_counts; i++)
    {
        sensors_temp[i] = sensors.getTempCByIndex(i);
        // Serial.println("Sensor " + String(i + 1) + ": " + String(sensors_temp[i]) + " *C");
        temperature_sum += sensors_temp[i];
    }
    ave_temperature = temperature_sum / sensor_counts;
    Serial.println("Average sensor: " + String(ave_temperature) + "°C");

    for (int i = 0; i < sensor_counts; i++)
    {
        // in case the conneciton is broken
        if (sensors_temp[i] < -50)
        {
            Serial.println("Sensor disconnected, incubator off");
            // analogWrite(HEATER_PIN, 0);
            sensors_error = true;
        }
    }

    // if sensors  has no error, proceed to change PID value
    if (!sensors_error)
    {
        // NOTE: optional use Xth sensor as the PID_input
        // PID_input = sensors_temp[0]
        // use ave_temperature as the PID input
        PID_input = ave_temperature;
        myPID.Compute();
        analogWrite(HEATER_PIN, PID_output);
        Serial.print("Heating effort is: ");
        Serial.println(PID_output);
    }
}

void serial_condition(String serial_input)
{
    //Serial.println(serial_input);
    // trim is needed as there is blank space and line break
    serial_input.trim();

    if (serial_input == "led_on" or serial_input == "LED_on")
    {
        // turn off the incubator to prevent current spike
        analogWrite(HEATER_PIN, 0);
        LED_colour(r, g, b);
        Serial.println("lights on");
    }
    else if (serial_input == "led_off" or serial_input == "LED_off")
    {
        LED_colour(0, 0, 0);
        Serial.println("lights off");
    }
    // LED_RGB=255,255,255
    else if (serial_input.substring(0, 7) == "LED_RGB" or serial_input.substring(0, 7) == "LED_rgb")
    {
        serial_input = serial_input.substring(8);
        r = (getValue(serial_input, ',', 0).toInt());
        g = (getValue(serial_input, ',', 1).toInt()); // turn the LED on (HIGH is the voltage level
        b = (getValue(serial_input, ',', 2).toInt()); // turn the LED on (HIGH is the voltage level
        LED_colour(r, g, b);
    }
    // move_opt=600
    // move_car=30 (degrees)
    // positive is toward the camera, negative is toward the endstop
    else if (serial_input.substring(0, 4) == "move")
    {
        String motor_type = serial_input.substring(5, 8);
        int distance = serial_input.substring(9).toFloat();
        move_stage(motor_type, distance);
    }

    // speed_car=500
    else if (serial_input.substring(0, 5) == "speed")
    {
        String motor_type = serial_input.substring(6, 9);
        int speed = serial_input.substring(10).toFloat();
        if (motor_type == "opt")
        {
            Serial.print("Changing the stepper_optics speed to: ");
            Serial.println(serial_input.substring(6).toFloat());
            if (speed == 0)
            {
                speed = 250;
            }
            speed_optics = speed;
            stepper_optics.setSpeed(speed_optics);
        }

        else if (motor_type == "car")
        {
            Serial.print("Changing the stepper_carousel speed to: ");
            Serial.println(serial_input.substring(6).toFloat());
            if (speed == 0)
            {
                speed = 250;
            }
            speed_carousel = speed;
            stepper_optics.setSpeed(speed_carousel);
        }
    }

    else if (serial_input == "pos")
    {
        Serial.print("Absolute optic stage position: ");
        Serial.print(absolute_pos_optics);
        Serial.println(" um");
        Serial.print("Absolute optic carousel position: ");
        Serial.print(absolute_pos_carousel);
        Serial.println("°");
    }

    else if (serial_input == "stop")
    {
        stepper_optics.setSpeed(250);
        stepper_optics.step(0);
        stepper_carousel.setSpeed(250);
        stepper_carousel.step(0);
    }

    // home_opt and home_car
    else if (serial_input.substring(0, 4) == "home")
    {
        String motor_type = serial_input.substring(5, 8);
        home_stage(motor_type);
    }

    // temp=37
    else if (serial_input.substring(0, 4) == "temp")
    {
        PID_setpoint = serial_input.substring(5).toFloat();
        if (PID_setpoint > 50)
        {
            Serial.println("Maximum temperature is 50 C");
            PID_setpoint = 50;
        }
        Serial.print("Temperature is set to ");
        Serial.print(PID_setpoint);
        Serial.println(" C");
    }
}

void LED_colour(int r, int g, int b)
{
    for (int i = 0; i < NUMPIXELS; i++)
    {
        // pixels.Color takes RGB values, from 0,0,0 up to 255,255,255
        LED.setPixelColor(i, LED.Color(r, g, b)); // Moderately bright green color.
        LED.show();                               // This sends the updated pixel color to the hardware.
        delay(5);
    }
}

void move_stage(String motor_type, float distance)
{
    // turn off the incubator to prevent current spike
    analogWrite(HEATER_PIN, 0);

    if (motor_type == "opt")
    {
        // DEBUG: the endstop was not connected, remove comment later
        //if (distance < 0 && digitalRead(END_STOP_PIN) == LOW)
        if (distance < 0 && digitalRead(END_STOP_PIN_OPT) == HIGH)
        {
            // if hit end_stop, and still trying to move towards end_stop
            Serial.println("already at the end stop");
            absolute_pos_optics = 0;
        }
        else
        {
            stepper_optics.setSpeed(speed_optics);
            // in python, using this line to start sleep
            Serial.println("Moving the motor, stop accepting commands");
            // convert distance in um  to steps
            int motor_steps = distance * stepper_optics_ratio;
            stepper_optics.step(motor_steps);
            // in Python,  this sentence indicated the motor has finished movement

            Serial.println("Finished the movement");

            absolute_pos_optics = absolute_pos_optics + distance;
            Serial.println("Move by: " + String(distance) + "um");
        }
        Serial.print("Optical stage absolute position: ");
        Serial.println(absolute_pos_optics);
    }
    else if (motor_type == "car")
    {
        stepper_carousel.setSpeed(speed_carousel);
        // convert degrees to steps
        int motor_steps = distance * stepper_carousel_ratio;
        // in python, using this line to start sleep
        Serial.println("Moving the motor, stop accepting commands..");

        stepper_carousel.step(motor_steps);
        // in Python,  this sentence indicated the motor has finished movement
        Serial.println("Finished the movement");
        // the range of degree - 0 to 360
        absolute_pos_carousel += distance;
        absolute_pos_carousel = fmod(absolute_pos_carousel, 360);

        Serial.println("Move by: " + String(distance) + "°");
        Serial.println("Carousel absolute position: " + String(absolute_pos_carousel) + "°");
    }
}

void home_stage(String motor_type)
{
    if (motor_type == "opt")
    {
        while (digitalRead(END_STOP_PIN_OPT) == HIGH)
        {
            move_stage("opt", -100);
        }
        // after hitting the end_stop, reset the absolute position
        absolute_pos_optics = 0;
    }
    else if (motor_type == "car")
    {
        while (digitalRead(END_STOP_PIN_CAR) == HIGH)
        {
            move_stage("car", 10);
        }
        // after hitting the end_stop, reset the absolute position
        absolute_pos_carousel = 0;
    }
    Serial.println("Stage homed, reset the absolute position");
}

// sammy's home made code to extract RGB value
String getValue(String data, char separator, int index)
{
    int found = 0;
    int strIndex[] = {0, -1};
    int maxIndex = data.length() - 1;

    for (int i = 0; i <= maxIndex && found <= index; i++)
    {
        if (data.charAt(i) == separator || i == maxIndex)
        {
            found++;
            strIndex[0] = strIndex[1] + 1;
            strIndex[1] = (i == maxIndex) ? i + 1 : i;
        }
    }
    return found > index ? data.substring(strIndex[0], strIndex[1]) : "";
}
